

plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
}

repositories {
    mavenCentral()
    
    maven {
        name = "WPI"
        url = "https://frcmaven.wpi.edu/artifactory/release/"
    }
}

ext {
    shuffleboard_version = "2020.1.2";
    publishing_version = "1.1.0";
    publishing_url = "${rootDir}/build/publishing"
}


task install_plugin(type: Copy) {
    from(tasks.named("jar"))
    into("${System.getProperty("user.home")}/Shuffleboard/plugins")
}

////////////////////////////
// Generate Version Info
////////////////////////////
sourceSets.main.java.srcDir "${buildDir}/generated/java/"
task generate_version() {
    def generated_dir = file("$buildDir/generated/java/com/gos/infinite_recharge/sd_widgets")
    generated_dir.mkdirs()
    def output_file = file("$generated_dir/PluginVersion.java")
    def file_template = """package com.gos.infinite_recharge.sd_widgets;


public class PluginVersion {
    public static final String VERSION = "${project.publishing_version}";
} 
"""
    
    doLast {
        if (output_file.exists()) {
            output_file.delete()
        }
        output_file.write(file_template)
    }
    
}
compileJava.dependsOn generate_version
////////////////////////////

dependencies {
    
    compile "org.openjfx:javafx-base:11:win"
    compile "org.openjfx:javafx-graphics:11:win"
    compile "org.openjfx:javafx-controls:11:win"
    compile "org.openjfx:javafx-fxml:11:win"
    
    
    compile 'edu.wpi.first.shuffleboard:shuffleboard:' + shuffleboard_version
    compile 'edu.wpi.first.shuffleboard:api:' + shuffleboard_version
    compile 'edu.wpi.first.shuffleboard.plugin:networktables:' + shuffleboard_version
    compile 'edu.wpi.first.shuffleboard:api:' + shuffleboard_version
    compile 'edu.wpi.first.shuffleboard.plugin:networktables:' + shuffleboard_version
}


apply from: "../styleguide.gradle"

publishing {
    repositories {
        maven {
            url publishing_url
        }
    }
    publications {
        application(MavenPublication) {
            groupId = "com.gos.infinite_recharge.sd_widgets"
            artifactId = "RobotWidget"
            version = publishing_version
            
            artifact jar
        }
    }
}
